apply plugin: 'groovy'

version = '0.1'

repositories {
  mavenCentral()
}

dependencies {
  groovy 'org.codehaus.groovy:groovy:2.0.5'
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile fileTree( dir:'build/libs/', includes:[ "groovy-common-extensions-${version}.jar" ] )
}

group = 'com.bloidonia'

def metaServices = new File( 'src/main/groovy/META-INF/services' )

task generateModule << {
  def extensionFiles = sourceSets.main.groovy.with { g ->
    g.findAll {
      it.parentFile.name == 'extensions' && it.name.endsWith( 'groovy' )
    }.collect {
      it.toString().replaceAll( File.separator, '.' )
    }.collect {
      it - ( g.srcDirs as List )[0].toString().replaceAll( File.separator, '.' )
    }.collect {
      it[ 1..-8 ]
    }.join( ',' )
  }
  metaServices.mkdirs()
  new File( metaServices, 'org.codehaus.groovy.runtime.ExtensionModule' ).withWriter { w ->
    w.writeLine 'moduleName=groovy-stream'
    w.writeLine "moduleVersion=$version"
    w.writeLine "extensionClasses=$extensionFiles"
    w.writeLine 'staticExtensionClasses='
  }
}

jar {
  from( metaServices ) {
    include 'org.codehaus.groovy.runtime.ExtensionModule' into( 'META-INF/services' )
  }
}

// Hook up dependencies
compileGroovy.dependsOn( generateModule )
test.dependsOn( jar )

task wrapper(type: Wrapper) {
  gradleVersion = '1.2'
}