apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

version = '0.4'

repositories {
  mavenCentral()
}

dependencies {
  groovy 'org.codehaus.groovy:groovy-all:2.0.5'
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile fileTree( dir:'build/libs/', includes:[ "groovy-common-extensions-${version}.jar".toString() ] )
}

group = 'com.bloidonia'

task generateModule << {
  def extensionFiles = sourceSets.main.groovy.with { g ->
    g.findAll {
      it.parentFile.name == 'extensions' && it.name.endsWith( 'groovy' )
    }.collect {
      it.toString().replaceAll( File.separator, '.' )
    }.collect {
      it - ( g.srcDirs as List )[0].toString().replaceAll( File.separator, '.' )
    }.collect {
      it[ 1..-8 ]
    }.join( ',' )
  }
  new File( "$buildDir/classes/main/META-INF/services" ).with { f ->
    f.mkdirs()
    new File( f, 'org.codehaus.groovy.runtime.ExtensionModule' ).withWriter { w ->
      w.writeLine 'moduleName=groovy-stream'
      w.writeLine "moduleVersion=$version"
      w.writeLine "extensionClasses=$extensionFiles"
      w.writeLine 'staticExtensionClasses='
    }
  }
}

// Hook up dependencies
jar.dependsOn( generateModule ) 
test.dependsOn( jar )

groovydoc {
  use             = true
  destinationDir  = new File( "${project.buildDir.path}/groovydoc", project.version )
  header = '<a href="https://github.com/timyates/groovy-common-extensions">groovy-common-extensions</a>'
  footer = null

  link 'http://download.oracle.com/javase/6/docs/api/',                 'java.'
  link 'http://groovy.codehaus.org/api/',                               'groovy.', 'org.codehaus.groovy.'
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier 'javadoc'
  from groovydoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives groovydocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.3'
}
